#Область СохранениеЧтениеЭксель

//Загружает Эксель в таблицу значений, наименования колонок берёт из первой строки Эксель
Функция ЗагрузитьЭксель(СтрПутьКФайлу)
	
	ТабЗнач = Новый ТаблицаЗначений;
	
	//Создание менеджера Excel
	ЭксельОбъект = Новый COMОбъект("Excel.Application");
	//Открытие книги
	КнигаЭксель = ЭксельОбъект.WorkBooks.Open(СтрПутьКФайлу);
	//Получение листа
	ЛистЭксель = КнигаЭксель.Worksheets(1);
	
	КоличествоКолонок = ЛистЭксель.Cells(1, 1).SpecialCells(11).Column;
	//Добавление колонок
	Для номерКолонки = 1 По КоличествоКолонок Цикл
		Если Не ПустаяСтрока(ЛистЭксель.Cells(1, номерКолонки).Value) Тогда			
			новаяКолонка = ТабЗнач.Колонки.Добавить(УдалитьНедопустимыеСимволы(ЛистЭксель.Cells(1, номерКолонки).Value));
			новаяКолонка.Заголовок = ЛистЭксель.Cells(1, номерКолонки).Value;
		КонецЕсли;
	КонецЦикла;

	
	КоличествоКолонок = ТабЗнач.Колонки.Количество();
	
	КоличествоСтрок = ЛистЭксель.Cells(1, 1).SpecialCells(11).Row;
	//Добавление нужного Количества строк
	Для номерСтроки = 1 По КоличествоСтрок Цикл
		ТабЗнач.Добавить();
	КонецЦикла;
	//Добавление всех строк
	Для номерКолонки = 1 По КоличествоКолонок Цикл
		массивСтрокЭксель = СчитатьКолонкуЭксель(номерКолонки, ЛистЭксель);
		ТабЗнач.ЗагрузитьКолонку(массивСтрокЭксель, номерКолонки - 1); 
		
	КонецЦикла;
	
	//Закрытие книги
	КнигаЭксель.Close(0);
	//Высвобождение памяти
	ЭксельОбъект.Quit();
	
	Возврат ТабЗнач;
КонецФункции


//Получает таблицу значений и сохраняет её через COMSafeArray 
Процедура БыстроеСохранениеТабличногоДокументаВЭксель(аргТабЗначДляВыгрузки, аргСтрПутьКФайлу)
	
	ВсегоКолонок = аргТабЗначДляВыгрузки.Колонки.Количество();
	ВсегоСтрок = аргТабЗначДляВыгрузки.Количество();	
	
	масКом = Новый COMSafeArray("VT_Variant", ВсегоКолонок, ВсегоСтрок + 1);
	
	//Наименование шапки колонок
	Для индексКолонки = 0 По ВсегоКолонок - 1 Цикл
		масКом.SetValue(индексКолонки, 0, аргТабЗначДляВыгрузки.Колонки[индексКолонки].Заголовок);
	КонецЦикла;		

	
	Для индексКолонки = 0 По ВсегоКолонок - 1 Цикл
		Для индексСтроки = 0 По ВсегоСтрок - 1 Цикл
			масКом.SetValue(индексКолонки, индексСтроки + 1, аргТабЗначДляВыгрузки[индексСтроки][индексКолонки]);
		КонецЦикла;		
	КонецЦикла;
	
	
	//Создание менеджера Excel
	ЭксельОбъект = Новый COMОбъект("Excel.Application");
	//Отключение предупреждений
	ЭксельОбъект.DisplayAlerts = Ложь;
	//Создание новой книги
	КнигаЭксель = ЭксельОбъект.WorkBooks.Add();
	//Получение листа
	ЛистЭксель = КнигаЭксель.Worksheets(1);
	
	Для номерКолонки = 0 По ВсегоКолонок - 1 Цикл
		ЛистЭксель.Columns(номерКолонки + 1).ColumnWidth = РасчитатьДлинуКолонки(аргТабЗначДляВыгрузки[1][номерКолонки]);
	КонецЦикла;
	
	ЛистЭксель.Range(ЛистЭксель.Cells(1, 1), ЛистЭксель.Cells(ВсегоСтрок + 1, ВсегоКолонок)).Value = масКом;
	
	//Сохранение Excel
	КнигаЭксель.SaveAs(аргСтрПутьКФайлу);
	//Высвобождение памяти
	ЭксельОбъект.Quit();
	
КонецПроцедуры


Функция СчитатьКолонкуЭксель(АргНомерКолонки, АргЛистЭксель)
	масЗначенияКолонки = Новый массив();
	перКоличествоСтрок = АргЛистЭксель.Cells(1, АргНомерКолонки).SpecialCells(11).Row;
	//Получение значения ячейки
	Для Счетчик = 1 По перКоличествоСтрок Цикл
		
		ЗначениеЯчейкиЭксель = АргЛистЭксель.Cells(Счетчик, АргНомерКолонки).Value;
		масЗначенияКолонки.Добавить(Формат(ЗначениеЯчейкиЭксель, "ЧГ=100"));	
	КонецЦикла;
	
	Возврат масЗначенияКолонки
	
КонецФункции


Функция РасчитатьДлинуКолонки(аргПер)
	Если Не ТипЗнч(аргПер) = Тип("Строка") Тогда
		стрДляПроверки = Строка(аргПер);
	Иначе
		стрДляПроверки = аргПер;
	КонецЕсли;
	
	длинаСтроки = СтрДлина(стрДляПроверки);
	
	Если длинаСтроки < 60 Тогда
		Возврат длинаСтроки;
	Иначе
		Возврат 60;
	КонецЕсли;
		
КонецФункции

#КонецОбласти


#Область РаботаСТекстовымиФайлами

Функция ЗагрузитьТекстВТаблицу(АргСтрПутьКФайлу, аргСтрРазделитель)
	
	ТабЗнач = Новый ТаблицаЗначений;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(АргСтрПутьКФайлу);
	
	колонки = СтрРазделить(ТекстовыйДокумент.ПолучитьСтроку(1), аргСтрРазделитель);
	
	Для Каждого элМас Из колонки Цикл
		ТабЗнач.Колонки.Добавить(элМас);		
	КонецЦикла;
	
	
	Для номерСтроки = 2 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		новаяСтрока = ТабЗнач.Добавить();
		
		масЭлементы = СтрРазделить(ТекстовыйДокумент.ПолучитьСтроку(номерСтроки), аргСтрРазделитель, Истина); 
		
		Для индексКолонки = 0 По ТабЗнач.Колонки.Количество() - 1 Цикл
			новаяСтрока[индексКолонки] = масЭлементы[индексКолонки];
			
		КонецЦикла;
		
	КонецЦикла; 
	
	Возврат ТабЗнач; 
	
КонецФункции

#КонецОбласти


Функция УдалитьНедопустимыеСимволы(аргСтрИмяКолонки)
	переделаннаяСтрока = СтрЗаменить(аргСтрИмяКолонки, " ", "_");
	переделаннаяСтрока = СтрЗаменить(переделаннаяСтрока, ":", "");
	переделаннаяСтрока = СтрЗаменить(переделаннаяСтрока, "(", "");
	переделаннаяСтрока = СтрЗаменить(переделаннаяСтрока, ")", "");
	переделаннаяСтрока = СтрЗаменить(переделаннаяСтрока, "\", "");
	переделаннаяСтрока = СтрЗаменить(переделаннаяСтрока, "/", "");
	переделаннаяСтрока = СтрЗаменить(переделаннаяСтрока, "<", "");
	переделаннаяСтрока = СтрЗаменить(переделаннаяСтрока, ">", "");
	переделаннаяСтрока = СтрЗаменить(переделаннаяСтрока, "*", "");

	Возврат переделаннаяСтрока;

КонецФункции

